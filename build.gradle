buildscript {
    ext.kotlin_version = '1.2.0'
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.0-alpha05'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}
allprojects {
    repositories {
        jcenter()
        google()
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

dependencies {
    compile 'com.google.protobuf.nano:protobuf-javanano:3.1.0'

    compile 'com.android.support:support-core-utils:27.0.2'
    compile 'com.android.support:support-media-compat:27.0.2'
    compile 'com.android.support:recyclerview-v7:27.0.2'

    compile 'net.hockeyapp.android:HockeySDK:5.0.2'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'

    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.2'

    defaultConfig {
        applicationId "ca.yyx.hu"
        minSdkVersion 16
        targetSdkVersion 16

        manifestPlaceholders = [HOCKEYAPP_APP_ID: "$HEADUNIT_HOCKEYAPP_ID"]

        versionCode 22
        versionName "1.0"

        ndk {
            moduleName "hu_jni"
        }

        externalNativeBuild {
            cmake {
                cppFlags ""
                arguments "-DANDROID_TOOLCHAIN=clang"
                abiFilters "armeabi", "armeabi-v7a", "x86"
            }
        }
    }

    externalNativeBuild {
        cmake{
            path "CMakeLists.txt"
        }
    }

    signingConfigs {
        debug {
            storeFile file(HEADUNIT_KEYSTORE_FILE)
            storePassword HEADUNIT_KEYSTORE_PASSWORD
            keyAlias HEADUNIT_KEY_ALIAS
            keyPassword HEADUNIT_KEY_PASSWORD
        }

        release {
            storeFile file(HEADUNIT_KEYSTORE_FILE)
            storePassword HEADUNIT_KEYSTORE_PASSWORD
            keyAlias HEADUNIT_KEY_ALIAS
            keyPassword HEADUNIT_KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            jniDebuggable true
            signingConfig signingConfigs.debug
        }
    }

    lintOptions {
        abortOnError false
    }

    task genSource(type: Exec) {
        println "Executing " + HEADUNIT_SCRIPT_FILE
        commandLine HEADUNIT_SCRIPT_FILE
    }

    preBuild.dependsOn genSource

    // workaround for otlin tests
    task copyTestClasses(type: Copy) {
        from "build/tmp/kotlin-classes/debugUnitTest"
        into "build/intermediates/classes/debug"
    }
    task copySdkClasses(type: Copy) {
        from "build/tmp/kotlin-classes/debug"
        into "build/intermediates/classes/debug"
    }
}
repositories {
    mavenCentral()
}

